include 'desktop', 'core', 'server', 'ios', 'annotations', 'tools', 'tests'

def use = { String name ->
    include(name)
    project(name).projectDir = new File(settingsDir, "../${name.substring(1).replace(":", "/")}")
}

def properties = new Properties()

if(new File(settingsDir, 'local.properties').exists() && System.getenv("JITPACK") != "true"){
    properties.load(new File(settingsDir, 'local.properties').newDataInputStream())

    if(properties.containsKey("sdk.dir")){
        include 'android'
    }else{
        println("No android SDK dir found Not loading Android module.")
    }
}else{
    println("No local.properties found. Not loading Android module.")
}

if(!hasProperty("release")){
    if(new File(settingsDir, '../Arc').exists()){
        use(':Arc')
        use(':Arc:arc-core')
        use(':Arc:extensions')
        use(':Arc:extensions:freetype')
        use(':Arc:extensions:recorder')
        use(':Arc:extensions:arcnet')
        use(':Arc:extensions:packer')
        use(':Arc:backends')
        use(':Arc:backends:backend-sdl')
        use(':Arc:backends:backend-lwjgl3')
        use(':Arc:backends:backend-android')
        use(':Arc:backends:backend-robovm')
        use(':Arc:backends:backend-headless')
    }
    
    if(new File(settingsDir, '../Mindustry-Debug').exists()){
        include(":debug")
        project(":debug").projectDir = new File(settingsDir, "../Mindustry-Debug")
    }
}else{
    println("Not including local repositories.")
}
